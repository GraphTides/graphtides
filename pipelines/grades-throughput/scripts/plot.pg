#!/usr/bin/gnuplot

set output "plot.pdf"
set terminal pdfcairo size 6cm,2.2cm font ",8"

set offset -.3,-.3,.2,.2

set boxwidth .6
set style fill solid 0.50 border 0

set logscale y
set ytics ("10k" 10000, "20k" 20000, "40k" 40000, "80k" 80000, "160k" 160000, "320k" 320000)
set grid ytics

set ylabel "throughput [events/s]"
set xlabel "target throughput [events/s]"

set key off
set label "pipe" at 1.45,480000
set label "tcp" at 5.7,480000

plot '/input/data-median-90perz-pipe.dat' using ($0 * .7):2 with boxes ls 1 lc rgb "#56AA1C" title "pipe",\
     ''                                   using ($0 * .7):4:3:4:4:(.3):xticlabels(strcol(1)[(1+strstrt(strcol(1), "/")):strlen(strcol(1))-3].'k') with candlesticks notitle whiskerbars ls 1,\
     '/input/data-median-90perz-tcp.dat'  using ($0 * .7 + 4.2):2 with boxes ls 1 lc rgb "#DF6D07" title "tcp",\
     ''                                   using ($0 * .7 + 4.2):4:3:4:4:(.3):xticlabels(strcol(1)[(1+strstrt(strcol(1), "/")):strlen(strcol(1))-3].'k') with candlesticks notitle whiskerbars ls 1
